import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id 'java'
  id "com.google.protobuf" version "0.8.17"
}

version '0.1.0'

repositories {
  mavenCentral()
}

dependencies {
  implementation "com.google.protobuf:protobuf-java:$protobufJavaVersion"
}

sourceSets {
  main {
    java {
      srcDirs = [
        "src/main/java",
        "${buildDir}/generated/source/proto/main/java",
        "${buildDir}/generated/source/proto/main/grpc"
      ]
    }
  }
}

def extension = Os.isFamily(Os.FAMILY_WINDOWS) ? '.cmd' : ''

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.11.4"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.29.0'
    }
    "ts" {
      def binary = project.projectDir.toPath().resolve(
        "node_modules/.bin/protoc-gen-ts${extension}")
      path = binary
    }
  }
  generateProtoTasks {
    all()*.plugins {
      "ts" {
        option 'service=grpc-web'
        outputSubDir = 'js'
      }
    }
    all()*.builtins {
      "js" {
        option 'import_style=commonjs'
        option 'binary'
        outputSubDir = 'js'
      }
    }
  }
}

apply from: './package.gradle'
