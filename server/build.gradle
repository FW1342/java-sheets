plugins {
  id 'java'
  id 'application'
}

group 'dev.jsheets'
version '0.1.0'

repositories {
  mavenCentral()
}

sourceCompatibility = 16
targetCompatibility = 16

ext {
  mongoDbDriverVersion = '4.3.2'
  cfg4jVersion = '4.4.1'
  slf4jVersion = '1.7.32'
  javalinVersion = '4.1.1'
  argsParseVersion = '0.9.0'
  javaxAnnotationVersion = '1.3.2'
  guiceVersion = '5.0.1'
}

dependencies {
  implementation project(':protocol')
  implementation project(':evaluation')
  implementation "org.apache.curator:curator-x-discovery:$curatorVersion"
  implementation "io.micrometer:micrometer-core:$micrometerVersion"
  implementation "org.mongodb:mongodb-driver-sync:$mongoDbDriverVersion"
  implementation "com.ecwid.consul:consul-api:$consuleClientVersion"
  implementation "io.javalin:javalin:$javalinVersion"
  implementation "org.slf4j:slf4j-simple:$slf4jVersion"
  implementation "com.google.protobuf:protobuf-java-util:$protobufJavaVersion"
  implementation "net.sourceforge.argparse4j:argparse4j:$argsParseVersion"
  implementation "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
  implementation "com.google.flogger:flogger:$floggerVersion"
  implementation "com.google.flogger:flogger-slf4j-backend:$floggerVersion"
  implementation "com.google.inject:guice:$guiceVersion"
  implementation "io.grpc:grpc-netty:$grpcVersion"
  implementation "io.grpc:grpc-stub:$grpcVersion"
  implementation "io.grpc:grpc-core:$grpcVersion"
  implementation "io.grpc:grpc-protobuf:$grpcVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitPlatformVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitPlatformVersion"
}

test {
  useJUnitPlatform()
}

mainClassName = 'jsheets.server.App'

task copyDependencies(type: Copy) {
  from configurations.runtimeClasspath
  into "$buildDir/libs"
}

private def listClassPath() {
  return configurations.runtimeClasspath.collect { "libs/${it.name}" }.join(" ")
}

processResources {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
  from ('../website/build/') {
    into 'static'
  }
}

jar {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
  archiveName 'app.jar'
  from ('../website/build/') {
    into 'static'
  }
  manifest {
    attributes(
      "Main-Class": "jsheets.server.App",
      "Class-Path": listClassPath()
    )
  }
}

def dockerImageName = "ehenoma/jsheets"
def dockerImageTag = System.getenv("TARGET_IMAGE_TAG") || "latest"
def dockerImage = "$dockerImageName:$dockerImageTag"

task buildDocker(type: Exec) {
  dependsOn copyDependencies, build
  workingDir "$projectDir"
  commandLine "docker", "build", "--rm", ".", "-t", dockerImage, "-f", "./server/deploy/Dockerfile"
}

task justRunDocker(type: Exec) {
  workingDir "$projectDir"
  commandLine "docker", "run", dockerImage, "-p", "8080:8080"
}

task runDocker(type: Exec) {
  dependsOn buildDocker
  workingDir "$projectDir"
  commandLine "docker", "run", dockerImage, "-p", "8080:8080"
}

